// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/String.jack

/**
 * Represents a String object. Implements the String type.
 */
class String {
	field int str_len;
	field Array buffer;
	field int buffer_len;

    /** Constructs a new empty String with a maximum length of maxLength. */
    constructor String new(int maxLength) {
		if(maxLength < 1) {
			let maxLength = 1;
		}
		let str_len = 0;
		let buffer = Array.new(maxLength);
		let buffer_len = maxLength;
		return this;
    }

    /** De-allocates the string and frees its space. */
    method void dispose() {
		do Array.dispose(buffer);
		return;
    }

    /** Returns the current length of this String. */
    method int length() {
		return str_len;
    }

    /** Returns the character at location j. */
    method char charAt(int j) {
		return buffer[j];
    }

    /** Sets the j'th character of this string to be c. */
    method void setCharAt(int j, char c) {
		let buffer[j] = c;
		return;
    }

    /** Appends the character c to the end of this String.
     *  Returns this string as the return value. */
    method String appendChar(char c) {  //index of the array begin with zero, so that "str_len = 0" means buffer[0] is reliable;
		if(str_len < buffer_len){
			let buffer[str_len] = c;
			let str_len = str_len + 1;
		}
		return this;
    }

    /** Erases the last character from this String. */
    method void eraseLastChar() {
		if(str_len > 0){
			let str_len = str_len - 1;
		}
		return;
    }

    /** Returns the integer value of this String until the first non
     *  numeric character. */
    method int intValue() {
		var int int_val;
		var int i;
		var boolean neg_flag;
		let int_val = 0;
		if((buffer[0] = 45) & (str_len > 0)){
			let neg_flag = true;
			let i = 1;
		}
		else{
			let neg_flag = false;
			let i = 0;
		}
		while((i < str_len) & (buffer[i] > 47) & (buffer[i] < 58)){
			let int_val = int_val * 10 + (buffer[i] - 48);
			let i = i + 1;
		}
		if(neg_flag){
			return -int_val;
		}
		else{
			return int_val;
		}
    }

    /** Sets this String to hold a representation of the given number. */
    method void setInt(int number) {
		let str_len = 0;
		if(number < 0){
			let number = -number;
			do appendChar(45);
		}
		do set_int(number);
		return;
		}
	
	method void set_int(int number)
	{
		var int c;
		var int char_num;
		var int res;
		if(number > 10){
			let res = number / 10;
			let char_num = number - (res * 10);
			let c = char_num + 48;
			do set_int(res);
			do appendChar(c);
		}
		else{
			let c = number + 48;
			do appendChar(c);
		}
		return;
    }

    /** Returns the new line character. */
    function char newLine() {
		return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
		return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
		return 34;
	}
}
